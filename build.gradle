plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

sourceCompatibility = 1.8
group 'com.javaquery'
version '1.2.1'

repositories {
    mavenCentral()
}

dependencies {
    implementation('org.slf4j:slf4j-api:+')
    implementation('org.json:json:+')
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.13.2'

    testImplementation 'net.logstash.logback:logstash-logback-encoder:6.6'
    testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: '+'
    testImplementation('org.junit.jupiter:junit-jupiter:5.7.0')
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

jar {
    manifest {
        attributes(
                'Built-By': "Vicky Thakor",
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Import-Package': "org.json,org.slf4j,*"
        )
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'util'
            from components.java

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Java Util Library'
                description = 'Java util class to reduce boilerplate codes'
                url = 'https://github.com/javaquery/util'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://github.com/javaquery/util/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'javaquery'
                        name = 'Vicky Thakor'
                        email = 'vicky.thakor@javaquery.com'
                        timezone = '+05:30'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/javaquery/util.git'
                    developerConnection = 'scm:git:ssh://github.com/javaquery/util.git'
                    url = 'https://github.com/javaquery/util'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username "javaquery"
                password "******"
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}